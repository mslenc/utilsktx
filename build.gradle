buildscript {
    repositories {
        mavenCentral()
    }
}

plugins {
    id "java-library"
    id "org.jetbrains.kotlin.jvm" version "1.9.23"
    id "maven-publish"
    id "signing"
}

group "io.github.mslenc"
version "1.5.3"

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }
    withSourcesJar()
    withJavadocJar()
}

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.9.23"
    implementation "org.jetbrains.kotlin:kotlin-reflect:1.9.23"

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core-jvm:1.8.0"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-jdk8:1.8.0"

    api "org.slf4j:slf4j-api:1.7.36"

    testImplementation "junit:junit:4.13.2"
    testImplementation "ch.qos.logback:logback-classic:1.5.3"
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit:1.9.23"
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'utilsktx'
            from components.java

            pom {
                name = "utilsktx"
                description = "Common utilities for my *ktx series of libraries."
                url = "https://github.com/mslenc/utilsktx"

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id = 'mslenc'
                        name = 'Mitja Slenc'
                        email = 'mslenc@gmail.com'
                    }
                }

                scm {
                    connection = "scm:git:https://github.com/mslenc/utilsktx.git"
                    developerConnection = "scm:git:git@github.com:mslenc/utilsktx.git"
                    url = "https://github.com/mslenc/utilsktx"
                }
            }
        }
    }

    repositories {
        maven {
            url = "s3://mslenc-releases/maven2"
            credentials(AwsCredentials) {
                accessKey = awsAccessKeyMslenc
                secretKey = awsSecretKeyMslenc
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}
